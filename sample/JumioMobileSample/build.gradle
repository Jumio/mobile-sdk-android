buildscript {
	ext.kotlin_version = "1.3.72"
	repositories {
		google()
		jcenter()
	}
	dependencies {
		classpath "com.android.tools.build:gradle:3.6.2"
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
	}
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

repositories {
	google()
	jcenter()
	maven { url 'https://mobile-sdk.jumio.com' }
}

ext {
	SDK_VERSION = "3.6.2"
}

dependencies {
	implementation "com.jumio.android:core:${SDK_VERSION}@aar"
	implementation "com.jumio.android:bam:${SDK_VERSION}@aar"
	implementation "com.jumio.android:auth:${SDK_VERSION}@aar"
	implementation "com.jumio.android:nv:${SDK_VERSION}@aar"
	implementation "com.jumio.android:nv-mrz:${SDK_VERSION}@aar"
	implementation "com.jumio.android:nv-nfc:${SDK_VERSION}@aar"
	implementation "com.jumio.android:nv-ocr:${SDK_VERSION}@aar"
	implementation "com.jumio.android:nv-barcode:${SDK_VERSION}@aar"
	implementation "com.jumio.android:nv-barcode-vision:${SDK_VERSION}@aar"
	implementation "com.jumio.android:face:${SDK_VERSION}@aar"
	implementation "com.jumio.android:dv:${SDK_VERSION}@aar"

	//for core:
	implementation "androidx.appcompat:appcompat:1.1.0"
	implementation "androidx.room:room-runtime:2.2.3"
	implementation "androidx.localbroadcastmanager:localbroadcastmanager:1.0.0"

	//for nv:
	implementation "com.google.android.material:material:1.1.0"
	implementation "androidx.cardview:cardview:1.0.0"
	implementation "androidx.constraintlayout:constraintlayout:1.1.3"

	//only for nv-barcode-vision
	implementation ("com.google.android.gms:play-services-vision:19.0.0"){
		exclude group: 'com.android.support', module:'support-v4'
	}

	//only for nv-nfc
	implementation "org.jmrtd:jmrtd:0.7.18"
	implementation "org.ejbca.cvc:cert-cvc:1.4.6"
	implementation "org.bouncycastle:bcprov-jdk15on:1.64"
	implementation "net.sf.scuba:scuba-sc-android:0.0.18"

	//only for face
	implementation "com.facetec:zoom-authentication:8.0.11@aar"

	//Kotlin
	implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

	implementation fileTree(dir: 'libs', include: '*.jar')
}

android {
	compileSdkVersion 29
	buildToolsVersion "29.0.3"
	
	compileOptions {
		sourceCompatibility JavaVersion.VERSION_1_8
		targetCompatibility JavaVersion.VERSION_1_8
	}
	
	defaultConfig {
		versionCode 1
		versionName "Jumio Sample"
		minSdkVersion 19
		multiDexEnabled true
		vectorDrawables.useSupportLibrary true
	}
	
	lintOptions {
		abortOnError false
	}

	dexOptions {
		javaMaxHeapSize "4g"
		preDexLibraries true
	}

	buildTypes {
		debug {
		}
		release {
			minifyEnabled true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
		}
	}
}